'----------------------------------------------------------------
'command2.txtを自動生成するための日本語スクリプト
'----------------------------------------------------------------
'【ヘッダファイルの書式】
'コマンドとみなしヘルプを作成するのは、
'Int INT Function FUNCTION Str STR から始まる行についてである
'Function 関数名(引数) { 内容 } // コメント // 引数の説明
'Int 変数名 = 値 // コメント // 範囲
'Str 変数名 = 値 // コメント // 範囲

ファイル名=「stdmsg.h」
出力ファイル="command2.txt"

＊コマンド生成
TXT=""
ファイル名を、開く。
反復(
  LINE=それ
  HEAD=MID(LINE,1,3);
  もし、HEAD="//_"ならば(
    CMD=MID(LINE,4,100);
	TXT=TXT&"//{CMD}\n";
	続ける。
  )
  
  LINEの、" "までを、切り取る。大文字変換。トリム。CMD=それ
  NAME=""
  DESC=""
  ARG=""
  もし、CMD="FUNCTION"ならば、CMD_FUNC
  もし、(CMD="INT")||(CMD="STR")ならば、CMD_VAR
  もし、NAME<>""ならば(
    TXT=TXT&"{NAME},\"{ARG}\",\"{DESC}\"\n"
  )
)。
TXTを、出力ファイルへ、保存。
終わり。

＊CMD_FUNC
LINEの、"("までを、切り取る。トリム。NAME=それ
LINEの、")"までを、切り取る。トリム。ARG=それ;ARG=NAME&"("&ARG&")"
LINEの、"//"までを、切り取る。'捨て
LINEの、"//"までを、切り取る。トリム。DESC=それ
LINEの、"//"までを、切り取る。トリム。DD=それ; DESC=DESC&"/"&DD
戻る

＊CMD_VAR
LINEの、"="までを、切り取る。トリム。NAME=それ
LINEの、"//"までを、切り取る。'捨て
LINEの、"//"までを、切り取る。トリム。DESC=それ
LINEの、"//"までを、切り取る。トリム。ARG=それ
戻る

